import ETK.ETKUnit as Unit;
import ETK.ETKUtils as Utils;
import ETK.ETKTimer as Timer;
import ETK.ETKConstants as Const;
import ETK.ETKCommon as Common;
import Balance;
import Waves;

const TOWERS = EUDArray(1024);
var TOWER_COUNT = 0;
const Screen = StringBuffer(256);

// UTILS; MOVE

function findNewTargetInLocation(location, playerID) {
    var targetUnit = 0;
    var lowestHP = 0;
    var longestTimeOnMap = 0;
    foreach(ptr, unit : EUDLoopPlayerUnit(playerID)) {
        if (Utils.CUnitInLocation(unit, location)) {
            const unitAge = Unit.getKillCount(unit);
            if (targetUnit == 0 || unitAge > longestTimeOnMap) {
                longestTimeOnMap = unitAge;
                targetUnit = unit;
                const hp = Unit.getHitpoints(unit);
                if (targetUnit == 0 || hp < lowestHP) {
                    lowestHP = hp;
                    targetUnit = unit;
                }
            }
        }
    }
    return targetUnit;
}

object Offset {
    var x;
    var y;
};


function MakeOffset(x, y) : Offset {
    const offset = Offset.alloc();
    offset.x = x; 
    offset.y = y; 
    return offset;
}

object Image {
    var id;
    var x;
    var y;
    // var drawFunction
    // var drawWhenCloaked
    // var iscriptHeader
    // function drawAtPoint(x, y);
    // function drawAtUnit(x, y);
    // function drawAtLocaton(x, y);
};

function MakeImage(imageID, x, y) : Image {
    const image = Image.alloc();
    image.id = imageID;
    image.x = x; 
    image.y = y; 
    return image;
}

const TOWER_TYPES = EUDArray(64);

object TowerData {
    var baseDamage;
    var baseCooldown;
    var baseRange;
};

function MakeTowerData(baseDamage, baseRange, baseCooldown) : TowerData {
    const data = TowerData.alloc();
    data.baseDamage = baseDamage;
    data.baseCooldown = baseCooldown;
    data.baseRange = baseRange;
    return data;
}

object TowerImageData {
    var spriteImage : Image;
    var missileSpawnImage : Image;
    var missileImage : Image;
    var hitImage : Image;
};

function MakeTowerImageData(spriteImage, missileSpawnImage, missileImage, hitImage) : TowerImageData {
    const data = TowerImageData.alloc();
    data.spriteImage = spriteImage;
    data.missileSpawnImage = missileSpawnImage;
    data.missileImage = missileImage;
    data.hitImage = hitImage;
    return data;
}

object TowerType {
    var id;
    var data : TowerData;
    var imageData : TowerImageData;
    var fire: EUDFuncPtr (1, 0);
    var hit: EUDFuncPtr (3, 0);
};

function createTowerType(id, data: TowerData, imageData: TowerImageData) : TowerType {
    const towerType = TowerType.alloc();
    towerType.data = data;
    towerType.imageData = imageData;
    towerType.id = id;
    TOWER_TYPES[id] = towerType;
    return towerType;
}

object TowerUnit {
    var unit;
    var type: TowerType;
    var targetUnit;
};

function getTowerType(id): TowerType {
    return TOWER_TYPES[id];
}

function getTowerTypeFromUnit(towerUnit): TowerType {
    return TOWER_TYPES[Unit.getCustomValue(towerUnit)];
}

function isTowerValid(id) {
    return getTowerType(id) != 0 && id < TOWER_COUNT;
}

function createBaseTower(location, playerID) {
    const unitType = Const.Unit_TerranFlagBeacon;
    const towerUnit = Utils.createUnit(unitType, $L("SafeCreate"), $P8);
    MoveUnit(1, unitType, $P8, $L("SafeCreate"), location);
    if (Bring($P8, AtLeast, 1, unitType, $L("SafeCreate"))) {
        RemoveUnitAt(1, unitType, $L("SafeCreate"), $P8);
        eprintln("You can't build there.");
    } else {
        GiveUnits(1, unitType, $P8, location, playerID);
    }
}

function setTowerType(towerBaseUnit, towerID) {
    if (towerID == 99) {
        Utils.removeUnit(towerBaseUnit);
        return;
    }
    const towerUnit = Utils.replaceUnit(towerBaseUnit, Const.Unit_TerranMissileTurret);
    const towerType = getTowerType(towerID);

    const playerID = Unit.getPlayerID(towerUnit);
    Utils.moveUtilLocationToUnit(towerUnit);
    const cannonSpriteID = 487;
    const cannonImageID = towerType.imageData.spriteImage.id;
    Utils.setImageDrawFunction(cannonImageID, 1);
    Utils.setImageClickable(cannonImageID, 0);
    Utils.setSpriteImage(cannonSpriteID, cannonImageID);
    const cannonUnit = Utils.createUnitAtLocationWithOffset(Const.Unit_ProtossCorsair, Utils.UTILITY_LOCATION, $P8, towerType.imageData.spriteImage.x, towerType.imageData.spriteImage.y);
    Utils.setUnitColor(cannonUnit, playerID);
    Unit.setStatusFlags(cannonUnit, 0x04240201);
    Unit.setConnectedUnit(towerUnit, cannonUnit);

    Unit.setCustomValue(towerUnit, towerType.id);
    Unit.setRankIncrease(towerUnit, 0);
    Unit.setCommandCard(towerUnit, 100 + Unit.getPlayerID(towerUnit));
    Unit.setOrder(towerUnit, Const.Order_Nothing1_Unused);
}

function towerFire(towerUnit) {
    const cannonUnit = Unit.getConnectedUnit(towerUnit);
    const towerType = getTowerType(Unit.getCustomValue(towerUnit));
    const targetUnit = Unit.getTargetUnit(towerUnit);

    if (towerType.imageData.missileImage.id != Const.Image_None) {
        Utils.setSpriteImage(202, towerType.imageData.missileImage.id);
        Utils.setImageDrawFunction(towerType.imageData.missileImage.id, 1);
        Utils.setImageDrawIfCloaked(towerType.imageData.missileImage.id, 1);
    } else {
        Utils.setSpriteImage(202, Const.Image_Observer);
        Utils.setImageDrawFunction(towerType.imageData.missileImage.id, 0);
        Utils.setImageDrawIfCloaked(Const.Image_Observer, 0);
    }

    if (towerType.fire != 0) {
        towerType.fire(towerUnit);
    }

    const direction = Utils.getDirectionFromPosition32(towerUnit, Unit.getPositionX(targetUnit), Unit.getPositionY(targetUnit));
    Utils.moveUtilLocationToUnit(towerUnit);
    Utils.setUnitInitialDirection(Const.Unit_ProtossObserver, direction);
    const missileUnit = Utils.createUnitAtLocationWithOffset(Const.Unit_ProtossObserver, Utils.UTILITY_LOCATION, $P8, towerType.imageData.missileImage.x, towerType.imageData.missileImage.y);
    // Unit.setStatusFlags(missileUnit, 0x04250205);
    Utils.setUnitColor(missileUnit, Unit.getPlayerID(towerUnit));

    if (towerType.imageData.missileSpawnImage.id != Const.Image_None) {
        Utils.createImageSpriteAtUnitPosition(towerType.imageData.missileSpawnImage.id, cannonUnit);
    }
    Unit.setConnectedUnit(missileUnit, towerUnit);
    Unit.setOrder(missileUnit, Const.Order_Ignore_Normal);
    Unit.setTargetUnit(missileUnit, targetUnit);
}


function updateTower(towerUnit) {
    const towerType = getTowerTypeFromUnit(towerUnit);
    const enemyPlayerID = $P7;
    const towerRange = towerType.data.baseRange; //TODO: towerData.calculateRange();
    // Check if target is still within range, otherwise clear it and target a new unit
    if (Unit.isOrderTargetUnitValid(towerUnit)) {
        // Screen.printf("dist {} {}", towerRange, Utils.getUnitDistance(towerUnit, Unit.getTargetUnit(towerUnit)));
        if (Utils.getUnitDistance(towerUnit, Unit.getTargetUnit(towerUnit)) > towerRange) {
            // Screen.print("clearTargetUnit");
            Unit.clearTargetUnit(towerUnit);
            Unit.setOrder(towerUnit, Const.Order_Nothing1_Unused);         
        }
    } 

    if (!Unit.isOrderTargetUnitValid(towerUnit)) {
        // Check if there are enemy units nearby and target a new unit
        Utils.centerLocationOnUnit(Utils.LOCATION_ANYSIZE, towerUnit, towerRange * 2, towerRange * 2);
        Utils.refreshLocations();
        if (Bring(enemyPlayerID, AtLeast, 1, Const.Unit_AnyUnit, Utils.LOCATION_ANYSIZE)) {
            const enemyUnit = findNewTargetInLocation(Utils.LOCATION_ANYSIZE, enemyPlayerID );
            // Screen.printf("Bring {}", enemyUnit);
            if (enemyUnit != 0) {
                // Screen.print("setTargetUnit");
                Unit.setTargetUnit(towerUnit, enemyUnit);
                Unit.setOrder(towerUnit, Const.Order_Attack_Building);
            }
        }
    }


    //Firing
    if (Unit.getGroundWeaponCooldown(towerUnit) > towerType.data.baseCooldown) {
        Unit.setGroundWeaponCooldown(towerUnit, towerType.data.baseCooldown);       
    }

    if (Unit.getGroundWeaponCooldown(towerUnit) == 0
        && Unit.getOrder(towerUnit) == Const.Order_Attack_Building
        && Unit.isOrderTargetUnitValid(towerUnit)) {
        towerFire(towerUnit);
        Unit.setGroundWeaponCooldown(towerUnit, towerType.data.baseCooldown);   
    }

    if (Unit.getOrder(towerUnit) == Const.Order_Nothing2_Normal) {
        Unit.setOrder(towerUnit, Const.Order_Nothing1_Unused);         
    }
}




function missileHit(missileUnit) {
    const targetUnit = Unit.getTargetUnit(missileUnit);
    const towerUnit = Unit.getConnectedUnit(missileUnit);
    const targetUnitType = Unit.getUnitType(targetUnit);
    const towerType = getTowerTypeFromUnit(towerUnit);
    if (towerType.imageData.hitImage.id != Const.Image_None) {
        Utils.createImageSpriteAtUnitPosition(towerType.imageData.hitImage.id, targetUnit);
    }
    const damage = towerType.data.baseDamage + towerType.data.baseDamage * Unit.getRankIncrease(towerUnit);

    if (towerType.hit != 0) {
        towerType.hit(towerType, missileUnit, targetUnit);
    }
    const createLocationX = Unit.getPositionX(targetUnit);
    const createLocationY = Unit.getPositionY(targetUnit);
    const targetUnitX = Unit.getOrderTargetX(targetUnit);
    const targetUnitY = Unit.getOrderTargetY(targetUnit);
    const targetUnitPriority = Unit.getKillCount(targetUnit);
    if (Unit.damage(targetUnit, damage)) {
        SetResources(Unit.getPlayerID(towerUnit), 8, Utils.getUnitDestroyValue(targetUnitType), 0);
        const data = [targetUnitType, targetUnitX, targetUnitY, targetUnitPriority, targetUnit, createLocationX, createLocationY];
        Timer.add(6, data, EUDFuncPtr (2, 0) (function(data, intervalCount) {
            for (var i = 0; i < 2; i++) {
                const unitType = Waves.GetNextUnitType(targetUnitType);
                if (unitType != Const.Unit_None) {
                    const unitData = EUDArray.cast(data);
                    const targetUnitType = unitData[0];
                    const targetUnitX = unitData[1];
                    const targetUnitY = unitData[2];
                    const targetUnitPriority = unitData[3];
                    const targetUnit = unitData[4];
                    const createLocationX = unitData[5];
                    const createLocationY = unitData[6];
                    // Create unit offscreen
                    const unit = Utils.createUnit(unitType, Utils.OFFSCREEN_LOCATION, $P7);
                    Unit.setStatusFlags(unit, 0x00230005); // 5xxxx does not move
                    Utils.moveUnit(unit, Utils.moveUtilLocationToPoint(createLocationX, createLocationY));
                    Unit.setKillCount(unit, targetUnitPriority);
                    Unit.orderToPoint(unit, targetUnitX, targetUnitY);
                }
            }
        }));
        
    }
    Utils.removeUnit(missileUnit);
}

function load() {

    const defaultMissileFire = EUDFuncPtr (1, 0) (function(towerUnit) {
        
    });

    const defaultMissileHit = EUDFuncPtr (1, 0) (function(missileUnit) {
    });

    const t0 = createTowerType(0,
        MakeTowerData(50, 32*6, 6*1),
        MakeTowerImageData(
            MakeImage(Const.Image_STA_STS_Photon_Cannon_Overlay, 6, -12),
            MakeImage(Const.Image_None, 0, 0),
            MakeImage(Const.Image_Phase_Disruptor, 0, -16),
            MakeImage(Const.Image_Phase_Disruptor_Hit, 0, 0)
        )
    );

    const t1 = createTowerType(1,
        MakeTowerData(5, 32*6, 12*1),
        MakeTowerImageData(
            MakeImage(Const.Image_Maelstrom_Overlay_Small, 0, -16),
            MakeImage(Const.Image_None, 0, 0),
            MakeImage(Const.Image_Burst_Lasers_Hit, 0, -16),
            MakeImage(Const.Image_Yamato_Gun_Hit, 0, 0)
        )
    );
    t1.hit = EUDFuncPtr (3, 0) (function(towerType, missileUnit, targetUnit) {
        Utils.createOverlayForUnit(targetUnit, Const.Image_Flames2_Type3_Small, 64);
        Unit.setPlagueTimer(targetUnit, 64);
    });

    const t2 = createTowerType(2,
        MakeTowerData(1, 32*2, 12*1),
        MakeTowerImageData(
            MakeImage(Const.Image_Khalis, 0, -16),
            MakeImage(Const.Image_None, 0, 0),
            MakeImage(Const.Image_Hallucination_Death3, 0, 0),
            MakeImage(Const.Image_None, 0, 0)
        )
    );
    t2.hit = EUDFuncPtr (3, 0) (function(towerType, missileUnit, targetUnit) {
        const slowSpeed = 100;//Unit.getOriginalTopSpeed(targetUnit) / 10;
        if ( Unit.getTopSpeed(targetUnit) == slowSpeed) {
            return;
        }
        const duration = 64;
        Unit.setTopSpeed(targetUnit, slowSpeed);
        Utils.createOverlayForUnit(targetUnit, Const.Image_Acid_Spores_6_9_Overlay_Small, duration);
        Timer.add(duration, targetUnit, EUDFuncPtr (2, 0) (function(callbackUnit, intervalCount) {
            Unit.restoreOriginalTopSpeed(callbackUnit);
        }));
    });
}